DES_GOBSTG_SCRIPTFILE 99

STAGEFILE
// 以上为脚本必备内容，STAGEFILE表明当前的脚本类型，意为子关卡类型脚本，每帧都将调用本脚本，本类脚本为核心脚本
// 现在开始学习脚本内容

// 第一讲，HelloWorld

/////////////////////////////////////////////

# 100	// 教程1
// #后面的数字成为约束名字，是本脚本
// 100号子关卡是选择战难度后进入的第一个地方，一切由此开始
// 直到下一个#之间的内容都是属于这个约束名字的领域，其中可以对应一个或多个约束条件来决定执行哪段脚本

@ @@> 0
// @为约束条件的表达前缀，其后面可以跟随一个数字表示具体约束条件（仅在该数字帧执行脚本），也可以是@@开头的TimeFunction（在符合表达式的帧中均执行脚本）
// 不同约束条件指定的区域如果有重叠，则按照文字顺序依次执行其中的脚本
// 本TimeFunction表达式这是最简单实用的表达式之一，代表在本约束名字执行过程中，帧满足条件>0（即为任意时刻），则执行脚本

SD [player1] CHARA * 2
// CHARA函数返回玩家组别编号，每组两人，所以组中第一个人的编号为队伍编号*2，第二个人为第一个人编号+1
// 注意使用中括号[]括起自由变量的名字，代表取其地址，此句意为把第一人编号存入player1的自由变量地址中

SD [player2] player1 + 1
// 同样存储第二个人的编号，注意方法的参数可以用函数或表达式代替，而函数的参数不可以

IF CHARA - 3 !
// 考虑特殊情况，无双所在的组别只有一个人可以对话
// A - B !表达式的意义为A等于B，仅在A等于B的情况下A - B结果为0，使用!反转仅当反转数为0时结果才为1，所以该表达式为一个较为常用的取等表达式
// 满足表达式内容后进入IF模块，IF模块无论有多少行都应该放入大括号{}之中
{
	SD [player2] player1
// 重新设置变量player2的内容
}

CHATON CHARA
// 宣布一个对话事件的开始，如果针对每个组别执行相同的对话内容，则使用CHARA作为CHATON块的参数
// 正常的对话事件应该一式四份，CHATON后面应该对四个组别的编号，并且并列排布四个对话事件

	player1 0xff true
// 对话事件的初始化，对话左边用己方第一个人，右边不出现人，用true表示左边开始
	player1 true "ＨｅｌｌｏＷｏｒｌｄ！"
// true表示左边的人进行对话，内容为HelloWorld，必须全角，而相应的立绘为左边第一个人
	player2 true "请打开Ｒｅｓｏｕｒｃｅ＼　　Ｓｃｒｉｐｔ＿ｔｕｔｏｒｉａｌ＿０１＼Ｓｔａｇｅ＿Ｅ＿１．ｓｃｒｉｐｔ对照看。"
	player1 true "其余内容参考里面的注释吧！按Ｂａｃｋ返回或使用暂停，不然会一直循环的。"
CHATOFF
// 宣布对话事件结束

// 对话会反复循环的原因是在对话事件结束后，置CHATI计数为0xff，之后被置为0，重新开始循环
// 一般对话使用如下语句跳出对话并转入下一个单元，如果想验证，则取消/**/的注释

/*

IF CHATI - 0xff !
// 当CHATI函数返回的计数为0xff（即结束了对话）
{
	SETTIME 0
// 设置时间为0（即跳入下一个约束名字时约束条件为0+1=1）
	SETSCENE 101
// 设置转入的约束名字（子关卡），本例假设进入乱难度
// 对话结束后请看左下角的数字，应该由100更换成101了
// 这两句话可作为普通子关卡之间的硬连接，是一个无缝的直接跳转
}

*/

/////////////////////////////////////////////

// 以下内容为无注释的教程内容，方便阅读，但约束名字为-1，不会被执行
# -1

@ @@> 0

SD [player1] CHARA * 2
SD [player2] player1 + 1

IF CHARA - 3 !
{
	SD [player2] player1
}

CHATON CHARA

	player1 0xff true
	player1 true "ＨｅｌｌｏＷｏｒｌｄ！"
	player2 true "请打开Ｒｅｓｏｕｒｃｅ＼　　Ｓｃｒｉｐｔ＿ｔｕｔｏｒｉａｌ＿０１＼Ｓｔａｇｅ＿Ｅ＿１．ｓｃｒｉｐｔ对照看。"
	player1 true "其余内容参考里面的注释吧！按Ｂａｃｋ返回或使用暂停，不然会一直循环的。"
CHATOFF

IF CHATI - 0xff !
{
	SETTIME 0
	SETSCENE 101
}

/////////////////////////////////////////////

// 由于脚本的末尾必须不是注释，所以增加了一个无用的约束条件作为脚本结束
// 请继续游戏，这次选择乱难度
// 打开stage_N_1.script同步进行脚本
@ 1